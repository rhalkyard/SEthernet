Name        SEthernet Bus Interface and Address Decoder ;
PartNo      00 ;
Date        2023-10-23 ;
Revision    01 ;
Designer    rhalkyard ;
Company     none ;
Assembly    SEthernet Macintosh SE PDS Ethernet Card ;
Location    U2 ;
Device      g16v8a ;

/*
Address decoding and glue logic for SEthernet
*/

/* INPUTS */
PIN [1..8]      = [A23..16] ;   /* High byte of address */
PIN 9           = !AS ;         /* Address strobe from CPU */
/* PIN 10       = gnd ; */
PIN 11          = !ENC_INT ;    /* Interrupt from ENC624J600 */

/* IO USED AS INPUT */
PIN 13          = !LDS ;        /* Low byte data-strobe from CPU */
PIN 14          = !UDS ;        /* High byte data-strobe from CPU */

/* IO USED AS OUTPUTS */
PIN 12          = !IPL1 ;       /* CPU interrupt-level bit 1 */
/* PIN 15       = unused ; */
PIN 16          = B0SEL ;       /* Byte 0 select to ENC624J600 */
PIN 17          = B1SEL ;       /* Byte 1 select to ENC624J600 */
PIN 18          = !IPL0 ;       /* CPU interrupt-level bit 0 */
PIN 19          = CS ;          /* Chip select to ENC624J600 */
/* PIN 20       = vcc ; */

/* Select on a 64k space starting at 0x80 0000 */
select          = AS & [A23..16]:'h'80xxxx ;
CS              = select;

/* Generate byte-select lines for ENC624J600 */
B0SEL           = UDS & select ;    /* ENC624J600 byte 0 (AD7..AD0) = 68k upper byte (D15..D8) */
B1SEL           = LDS & select ;    /* ENC624J600 byte 1 (AD15..AD8) = 68k lower byte (D7..D0) */

/* Drive CPU interrupt line as open-collector */
IPL0            = ENC_INT ;
IPL0.oe         = ENC_INT ;
