Name        Bus ;
PartNo      00 ;
Date        2023-10-23 ;
Revision    01 ;
Designer    rhalkyard ;
Company     none ;
Assembly    SEthernet/30 Macintosh SE/30 Ethernet card ;
Location    U4 ;
Device      g16v8a ;

/* 
Bus-control logic for SEthernet/30
*/

/* INPUTS */
PIN 1       = EN_CS ;   /* ENC624J600 chip select, from U3 */
PIN 2       = !ROM_CS ; /* ROM chip select, from U3 */
PIN 3       = A0 ;      /* Bit 0 of CPU address bus */
/* PIN 4 = unused, grounded ; */
PIN 4       = WAIT ;
PIN 5       = SIZ0 ;    /* Access-size lines from CPU */
PIN 6       = SIZ1 ;
/* PIN 7 = unused, grounded ; */
/* PIN 8 = unused, grounded ; */
PIN 9       = RW ;      /* R/W line from CPU */
/* PIN 10   = gnd ; */
PIN 11      = !DS ;     /* Data strobe from CPU */

/* IO USED AS OUTPUTS */
PIN 12      = !DSACK0 ; /* Data acknowledge lines to CPU */
PIN 13      = !DSACK1 ;
PIN 14      = !ROM_OE ; /* ROM read-enable */
PIN 15      = !ROM_WE ; /* ROM write-enable */
PIN 16      = B0SEL ;   /* Byte-select lines to ENC624J600 */
PIN 17      = B1SEL ;

PINNODE 18  = SLOT ;    /* Feedback node for DSACK OE */
/* PIN 19 = unused ; */
/* PIN 20 = vcc ; */

field size  = [SIZ1..0] ;

SLOT = EN_CS # ROM_CS ;

ROM_OE = ROM_CS & RW ;
ROM_WE = ROM_CS & !RW ;

/* ROM access are always 8-bit (DSACK='b'01). ENC accesses are always 16-bit
(DSACK='b'10) */
DSACK0 = ROM_CS & !WAIT ;
DSACK1 = EN_CS & !WAIT ;

/* DSACK lines are open-collector; outputs must be tristated when we're not
selected */
DSACK0.oe = SLOT ;
DSACK1.oe = SLOT ;

/* 
Generate byte-select outputs for ENC accesses. The EN_CS term here is important!
Even with CS deasserted, the ENC624J600 doesn't seem to like having its
byte-selects wiggled unnecessarily.
*/
B0SEL = EN_CS & DS & !A0 ; /* B0SEL on all even accesses */
B1SEL = EN_CS & DS & (! size:'b'01 # A0) ; /* B1SEL on word and odd byte accesses */
