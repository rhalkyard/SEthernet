Name        Bodge ;
PartNo      00 ;
Date        2023-10-23 ;
Revision    01 ;
Designer    rhalkyard ;
Company     none ;
Assembly    SEthernet/30 Macintosh SE/30 Ethernet card ;
Location    bodge ;
Device      g16v8a ;

/* 
Wait-state generator bodge for SEthernet/30. To be attached to the board 'dead
bug' style and wired to points on board listed below
*/

/* INPUTS */
PIN 1       = CPUCLK ;  /* CPU clock from PDS connector, pin a38 */
PIN 2       = !ROM_CS ; /* ROM chip select, from U3, pin 12 */
PIN 3       = EN_CS ;   /* ENC624J600 chip select, from U3, pin 13 */
/* PIN 10   = gnd ; */
/* PIN 11   = !OE (gnd) ; */

/* IO USED AS OUTPUTS */
PIN 12      = WAIT ;   /* Wait-state output, to U4, pin 4 (break connection to ground first) */

/* PIN 13   = unused ; */
/* PIN 14   = unused ; */
/* PIN 15   = unused ; */
/* PIN 16   = unused ; */
/* PIN 17   = unused ; */

PINNODE [17..19] = [count2..0] ; /* Counter state, leave unconnected */

/* PIN 20 = vcc ; */

FIELD counter = [count2..0] ;

selected = EN_CS # ROM_CS ;

sequence counter {
    present 'd'0
        if selected next 'd'1 ;
        default next 'd'0;
    present 'd'1
        if selected next 'd'2 ;
        default next 'd'0;
    present 'd'2
        if selected next 'd'3 ;
        default next 'd'0;
    present 'd'3
        if selected next 'd'4 ;
        default next 'd'0;
    present 'd'4
        if selected next 'd'5 ;
        default next 'd'0;
    present 'd'5
        if selected next 'd'6 ;
        default next 'd'0;
    present 'd'6
        if selected next 'd'7 ;
        default next 'd'0;
    present 'd'7
        if selected next 'd'7 ;
        default next 'd'0;
}

/* Way more delay than we need, but it works */
WAIT = !selected ;
APPEND WAIT = selected & !(counter:'d'7) ;
